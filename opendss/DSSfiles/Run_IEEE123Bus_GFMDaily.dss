! Annotated "Run" file for the IEEE 123 Bus Test Case
! Modified for demonstrating the grid forming and grid following capabilites
! added to OpenDSS on 10/28/2022

Compile (IEEE123Master.dss)

! 'Compile' differs from "redirect" in that it changes the default directory 
! to the one in which the referenced file is located.
! 'Redirect' returns to the directory from which it was invoked.

! After compiling, the next series of script commands modify to the regulator control definitions to
! limit the maximum tap change to 1 per solution control iteration. In most cases, this will 
! allow the standard DSS solution algorithm to better represent an actual control.
! Note the time delay is also chnaged. This forces the regulator at the head of the feeder to move first.

RegControl.creg1a.maxtapchange=1  Delay=15  !Allow only one tap change per solution. This one moves first
RegControl.creg2a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg3a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg3c.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4b.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4c.maxtapchange=1  Delay=30  !Allow only one tap change per solution

! The default maximum control iterations is 10. With so many regulators each moving only one tap at a time,
! this is insufficient for the 123-bus test case, which will need 15 control iterations for the initial solution.
! Therefore, the 'MaxControlIter' option is set to 30 to be safe.

Set MaxControlIter=30

! Solve executes the solution for the present solution mode, which is "snapshot".

New EnergyMeter.Feeder Line.L115 1

! This case considers the default load shape to represent the load demand in time for daily simulations
! Adding DER for supporting the microgrid below switch 5

new Transformer.TSto1 phases=3 windings=2 buses=(StoBus 101) conns=(delta wye) kvs=(0.48 4.16) kvas=(1000 1000) taps='1 1' XHL=0.5
new Transformer.TPV1 phases=3 windings=2 buses=(PVBus 108) conns=(wye delta) kvs=(0.48 4.16) kvas=(1000 1000) taps='1 1' XHL=0.5

New "Storage.mystorage" phases=3 conn=delta bus1=StoBus kV=0.48 kva=800 kWrated=800 kWhrated=6000 %stored=100 %reserve=20 
~ %EffCharge=90 %EffDischarge=90 %IdlingkW=1 %R=50 %X=50 State=DISCHARGING kP=0.3 KVDC=0.700 PITol=0.1

New "PVSystem.myPV" phases=3 conn=delta bus1=PVBus kV=0.48 kva=200 pmpp=200 daily=pvshape %R=50 %X=50 kP=0.1 KVDC=0.700 PITol=0.1 

! Grid forming inverter mode requires an InvControl for monitoring the current
New InvControl.StoCtrl_Storage DERList=[Storage.mystorage] mode=GFM !eventlog=yes
New InvControl.StoCtrl_PV DERList=[PVSystem.myPV] mode=GFM !eventlog=yes

! Monitors

New "Monitor.pvmonitor" element=pvsystem.mypv mode=1 terminal=1 PPolar=no residual=no VIPolar=yes
New "Monitor.pvvi" element=pvsystem.mypv mode=0 terminal=1
New "Monitor.pvstatevars" element=pvsystem.mypv mode=3 terminal=1
New "Monitor.stomonitor" element=storage.mystorage mode=1 terminal=1 PPolar=no residual=no VIPolar=yes
New "Monitor.stovi" element=storage.mystorage mode=0 terminal=1
New "Monitor.stostatevars" element=storage.mystorage mode=3 terminal=1

! Grid voltage monitor for all nodes
New "Monitor.gridvoltages" element=line.l115 mode=0 terminal=1 VIPolar=yes

! LV side voltage monitors
New "Monitor.lv_storage_bus" element=transformer.tsto1 mode=0 terminal=1 VIPolar=yes  ! Storage LV bus (StoBus)
New "Monitor.lv_pv_bus" element=transformer.tpv1 mode=0 terminal=1 VIPolar=yes       ! PV LV bus (PVBus)
New "Monitor.lv_610_bus" element=transformer.xfm1 mode=0 terminal=2 VIPolar=yes      ! 610 LV bus (480V side)

calcv
solve
Buscoords BusCoords.dat   ! load in bus coordinates
Set LoadMult=0.2
edit storage.mystorage ControlMode=GFM SafeVoltage=0
// edit pvSystem.myPV ControlMode=GFM SafeVoltage=0
Edit Storage.mystorage kP=0.5
// Edit PVSystem.myPV %R=0.1 %X=0.1 kP=0.01
open line.sw1 terminal=2
! Starts in dynamics mode and goes for 200 ms
set mode=dynamics steptime=0.002 maxiterations=30 number=200
set loadshapeclass=daily
set time=(10,0)

solve
